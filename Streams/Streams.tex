% $Author$
% $Date$
% $Revision$

% HISTORY:
% 2007-03-30 - Cassou splits of Streams from Collection chapter
% 2007-07-05 - Cassou partial draft complete?
% 2007-08-02 - Stef pass
% 2007-08-16 - Cassou continues
% 2007-08-21 - Oscar edit
% 2007-08-21 - Cassou review
% 2009-07-07 - Oscar migrate to Pharo; fixed broken tests
% 2011-06-16 - NicoPaez started spanish translation

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\pagestyle{headings}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
	% \renewcommand{\nnbb}[2]{} % Disable editorial comments
	\sloppy
\fi
%=================================================================
\chapter{Streams}\chalabel{streams}

\ew{Streams are presented as a way to navigate collection. From my point of view, stream are important not to navigate collection, but to produce/consume data:
(a)	memory constraint. Data can not hold into memory and must be processed in a stream fashion, e.g: encryption
(b)	blocking IO. A stream is a nice abstraction to deal with, and the stream manages internally data availability, buffering, etc. to simplify the consumption/production of data
Only few streams have random access capability.}

\clsindexmain{Stream}
Los Streams son usados para iterar sobre secuencias de elementos como colecciones secuenciadas, archivos y streams de red.
Los Streams pueden ser de lectura, de escritura o de ambos.
Leer o escribir es siempre relativo a la posici\'on en el stream.
Los Streams pueden ser f\'acilmente convertidos en colecciones y vice versa.

\lr{"Streams can easily be converted into collections." I wouldn't say it like this, because it is not true for all streams (infinite streams). According to Kent Beck we should only talk about conversion when the same protocol is supported. Collections and Streams do not support the same protocol. (p. 249)}

%=============================================================
\section{Dos secuencias de elementos}
Una buena met\'afora para entender un stream es las siguiente: Un stream puede ser representado como dos secuencias de elementos: una secuencia de 
elementos pasados y una secuencias de elementos futuros.
El stream es posicionado entre las dos secuencias. Entender este modelo es importante ya que toda operaci\'on en \st se basa en el.
% The stream is positioned between the two

Por esta raz\'on, la mayoria de las classes \clsind{Stream} son subclases de \clsind{PositionableStream}.
\figref{_abcde} presenta un stream con cinco caracteres. Este stream est\'a es su posici\'on original, \ie no hay elementos anteriores. 
Puedes volver a esta posici\'on usando el mensaje \mthind{PositionableStream}{reset}.

\begin{figure}[ht]
\centerline{\includegraphics[scale=0.5]{_abcdeStef}}
\caption{Un stream posicionado en su comienzo.}
\figlabel{_abcde}
\vspace{.2in}
\end{figure}

Leer un elemento conceptualmente significa sacar el primer elemento de la secuencia de elementos futuros y ponerlo despues del \'ultimo en
la secuencia de elementos pasados. Luego de haber leido un elemento usando el mensaje \ct{next}, el estado de tu stream es el mostrado en \figref{a_bcde}.

\begin{figure}[ht]
\centerline{\includegraphics[scale=0.5]{a_bcdeStef}}
\caption{El mismo stream despues de la ejecuci\'on del m\'etodo \ct{next}: ell caracter \ct{a} est\'a ``en el pasado'' mientras \ct{b}, \ct{c}, \ct{d} y
 \ct{e} estan ``en el futuro''.}
\figlabel{a_bcde}
\vspace{.2in}
\end{figure}

Escribir un elemento significa reemplazar el primer elemento de la secuencia futura por el nuevo y moverlo hacia atras.
 \figref{ax_cde} muestra el estado del mismo stream despues de haber escrito a \ct{x} usando el mensaje \mthind{Stream}{nextPut:} \ct{anElement}.

\begin{figure}[h!t]
\centerline{\includegraphics[scale=0.5]{ax_cdeStef}}
\caption{El mismo stream despues de haber escrito una \ct{x}.}
\figlabel{ax_cde}
\vspace{.2in}
\end{figure}

%=============================================================
\section{Streams vs. colecciones}

El protocolo de colecci\'on soporte el almacenamiento, la remoci\'on y enumeraci\'on de los elementos de una colecci\'on, 
but no permite que estas operaciones sean entremezcladas.  Por ejemplo, si los elementos de una \clsind{OrderedCollection} son
procesados por un m\'etodo \mthind{OrderedCollection}{do:}, no es posible agregar o remover elementos desde el interior del bloque \ct{do:}.
El protocolo de colecci\'on tampoco ofrece formas de interar sobre dos colecciones al mismo tiempo, eligiendo cual colecci\'on avanza y cual no.
Procedimientos como estos requieren que un \'indice transversal o una posici\'on de referencia sea mantenida fuera de la propia colecci\'on:
este es exactamente el rol de \clsind{ReadStream}, \clsind{WriteStream} y \clsind{ReadWriteStream}.

Estas tres clases estan definidas para trabajar sobre alguna colecci\'on.
Por ejemplo, el siguiente fragmento de c\'odigo crea un stream sobre un int\'ervalo y luego lee dos elementos
\needlines{5}
\begin{code}{@TEST |r|}
r := ReadStream on: (1 to: 1000).
r next.   --> 1
r next.   --> 2
r atEnd.--> false
\end{code}

\ct{WriteStream}s pueden escribir datos en una colecci\'on:
\begin{code}{@TEST |w|}
w := WriteStream on: (String new: 5).
w nextPut: $a.
w nextPut: $b.
w contents. -->  'ab'
\end{code}

Tambi\'en es posible crear \ct{ReadWriteStream}s que soporte ambos protocolos de escritura y lectura.

El principal problema con \ct{WriteStream} y \ct{ReadWriteStream} es que solo soportan arreglos y cadenas de caracteres en \pharo. Esto estÃ¡ siendo
cambiado actualmente con el desarrollo de una nueva biblioteca llamada Nile, pero por ahora si intentas trabajar sobre otra clase de colecci\'on, obtendr\'as un error:

\needlines{3}
\begin{code}{}
w := WriteStream on: (OrderedCollection new: 20).
w nextPut: 12. -->  raises an error
\end{code}

Los Streams no estan solo pensados para colecciones, tambi\'en pueden ser usados con archivos o sockets.
El siguiente ejemplo crea un archivo llamado \ct{test.txt}, escribe dos cadenas de caracteres separadas por un retorno de carro
y cierra el archivo.

\needlines{3}
\begin{code}{}
StandardFileStream
  fileNamed: 'test.txt'
  do: [:str | str
                nextPutAll: '123';
                cr;
                nextPutAll: 'abcd'].
\end{code}
\cmindex{FileStream class}{fileNamed:do:}

Las siguientes secciones presentan los protocoles en mayor profundidad.

%=============================================================
\section{Streaming over collections}

Los streams son realmente \'utiles para trabajar con colecciones de elementos. Pueden ser usados para leer y escribir elementos en colecciones
Ahora exploraremos las caracteristicas de los streams para las colecciones.

%-----------------------------------------------------------------
\subsection{Leyendo colecciones}

Esta secci\'on presenta caracteristicas usadas para leer colecciones. Usar un stream para leer una colecci\'on provee un puntero a la colecci\'on.
Dicho puntero se mover\'a hacia adelante leyendo y podr\'as ubicarlo donde gustes.

La clase \clsindmain{ReadStream} deberia usarse para leer elementos de la colecci\'on.

Los m\'etodos \mthind{ReadStream}{next} ay \mthind{ReadStream}{next:} son utilizados para obtener uno o m\'as elementos de una colecci\'on.


\begin{code}{@TEST |stream|}
stream := ReadStream on: #(1 (a b c) false).
stream next. -->   1
stream next. -->   #(#a #b #c)
stream next. -->   false
\end{code}
\cmindex{PositionableStream class}{on:}

\begin{code}{@TEST |stream|}
stream := ReadStream on: 'abcdef'.
stream next: 0. -->   ''
stream next: 1. -->   'a'
stream next: 3. -->   'bcd'
stream next: 2. -->   'ef'
\end{code}

El mensaje \mthind{PositionableStream}{peek} es usado cuando quieres conocer el siguiente elemento en el stream sin avanzar.

\begin{code}{@TEST |stream negative number|}
stream := ReadStream on: '-143'.
negative := (stream peek = $-).    "look at the first element without reading it"
negative. --> true
negative ifTrue: [stream next].       "ignores the minus character"
number := stream upToEnd.
number. --> '143'
\end{code}
\noindent

%$

Este c\'odigo establece la variable booleana \ct{negative} de acuerdo al signo del n\'umero
en el stream y \ct{number} a su valor absoluto. El m\'etodo \mthind{ReadStream}{upToEnd} devuelve
todo desde la posici\'on actual hasta el final del stream y establece el stream hasta su final. 
Este c\'odigo puede ser simplificado usando \mthind{PositionableStream}{peekFor:}, que adelanta si el siguiente
elemento es igual al par\'ametro y no se mueve en otro caso.

\begin{code}{@TEST |stream negative number|}
stream := '-143' readStream.
(stream peekFor: $-) --> true
stream upToEnd         --> '143'
\end{code}
\noindent

%$
\ct{peekFor:} tambi\'en devuelve un booleano indicanto si el par\'ametro es igual al elemento.

Puede que hayas notado un nuevo modo de construir un stream en el ejemplo anterior: uno puede simplemente enviar 
\mthind{SequenceableCollection}{readStream} a una colecci\'on secuenciable para obtener un stream de lectura de una colecci\'on particular.
collection.

\paragraph{Posicionamiento.} Hay m\'etodos para posicionar el puntero del stream. Si tienes el \'indice, puedes
usarlo directamente usando \mthind{PositionableStream}{position:}. Puedes pedir la posici\'on usando \mthind{PositionableStream}{position}. 
Por favor recuerda que un stream no est\'a posicionado en un elemento, sino entre dos elementos. El \'indice correspondiente al comienzo del stream es 0.

Puedes obtener el estado del stream representado en \figref{ab_cde} con el siguiente c\'odigo

\begin{code}{@TEST |stream|}
stream := 'abcde' readStream.
stream position: 2.
stream peek --> $c
\end{code}
%$

\begin{figure}[h!t]
\centerline{\includegraphics[scale=0.5]{ab_cdeStef}}
\caption{Un stream en la posici\'on 2}
\figlabel{ab_cde}
\vspace{.2in}
\end{figure}

Para posicionar el stream al principio o al final, puedes usar \mthind{PositionableStream}{reset} \'o \mthind{PositionableStream}{setToEnd}.
\mthind{PositionableStream}{skip:} y \mthind{PositionableStream}{skipTo:} son usados para
adelantar hasta una posici\'on relativa a la posici\'on actual: \ct{skip:}
toma un n\'umero como par\'ametro y saltea ese n\'umero de elementos, mientras que
\ct{skipTo:} saltea todos los elementos en el stream hasta que encuentra un elemento
igual a su par\'ametro. Notar que posiciona el stream despu\'es del elemento encontrado.

\begin{code}{@TEST |stream|}
stream := 'abcdef' readStream.
stream next.        --> $a    "stream is now positioned just after the a"
stream skip: 3.                           "stream is now after the d"
stream position.  -->   4
stream skip: -2.                          "stream is after the b"
stream position.  --> 2
stream reset.
stream position.  --> 0
stream skipTo: $e.                      "stream is just after the e now"
stream next.        --> $f
stream contents. --> 'abcdef'
\end{code}
%$

Como puedes ver, la letra \ct{e} ha sido salteada. 

El m\'etodo \mthind{PositionableStream}{contents} siempre devuelve una copia completa del stream.

\paragraph{Testing.} Algunos m\'etodos te permiten probar el estado del stream actual:
%Traducimos Testing?
\mthind{PositionableStream}{atEnd} devuelve verdadero si y solo si no hay m\'as elementos que puedan ser le\'idos, 
mientras que \mthind{PositionableStream}{isEmpty} devuelve verdadero si y solo si no hay m\'as elementos en la colecci\'on.

Aqu\'i hay una posible implementaci\'on de un algoritmo usando \ct{{atEnd} que toma dos colecciones ordenadas como 
par\'ametros y las mezcla, creando otra colecci\'on ordenada. 

\needlines{4}
\begin{code}{@TEST |stream1 stream2 result|}
stream1 := #(1 4 9 11 12 13) readStream.
stream2 := #(1 2 3 4 5 10 13 14 15) readStream.

"The variable result will contain the sorted collection."
result := OrderedCollection new.
[stream1 atEnd not & stream2 atEnd not]
  whileTrue: [stream1 peek < stream2 peek
    "Remove the smallest element from either stream and add it to the result."
    ifTrue: [result add: stream1 next]
    ifFalse: [result add: stream2 next]].

"One of the two streams might not be at its end. Copy whatever remains."
result
  addAll: stream1 upToEnd;
  addAll: stream2 upToEnd.

result. -->   an OrderedCollection(1 1 2 3 4 4 5 9 10 11 12 13 13 14 15)
\end{code}

%-----------------------------------------------------------------
\subsection{Escribiendo colecciones}

Ya hemos visto como leer una colecci\'on iterando sobre sus elementos, usando un \ct{ReadStream}. 
Ahora aprenderemos como crear colecciones usando \clsindmain{WriteStream}{}s.

Los \ct{WriteStream}s son \'utiles para agregar muchos datos a una colecci\'on en varias posiciones. A menudo son usados para construir cadenas de caracteres basadas en partes est\'aticas o din\'amicas, como en este ejemplo:

\begin{code}{NB: can't be tested due to the changing number of classes}
stream := String new writeStream.
stream
  nextPutAll: 'This Smalltalk image contains: ';
  print: Smalltalk allClasses size;
  nextPutAll: ' classes.';
  cr;
  nextPutAll: 'This is really a lot.'.

stream contents. --> 'This Smalltalk image contains: 2322 classes.
This is really a lot.'
\end{code}

Esta t\'ecnica es usada en diferentes implementaciones del m\'etodo \ct{printOn:} por ejemplo.
Hay una manera m\'as simple y eficiente de crear streams, si solo estas interesado en el contenido
del stream:

\begin{code}{@TEST |string|}
string := String streamContents:
		[:stream |
			stream
                 print: #(1 2 3);
                 space;
                 nextPutAll: 'size';
                 space;
                 nextPut: $=;
                 space;
                 print: 3.	].
string. -->   '#(1 2 3) size = 3'
\end{code}
%$
El m\'etodo \mthind{SequenceableCollection class}{streamContents:} \seclabel{streamContents} crea una colecci\'on y un stream
en esa colecci\'on por ti. Luego ejecuta el bloque que le has indicado, pasandoo el stream como par\'ametro. Cuado el bloque
termina, \ct{streamContents:} devuelve el contenido de la colecci\'on.

Los siguientes m\'etodos \ct{WriteStream} son especialmente \'utiles en este contexto:

\begin{description}
\item[\lct{nextPut:}] agrega el par\'ametro al stream;
\item[\lct{nextPutAll:}] agrega cada elemento de la colecci\'on, pasado como par\'ametro, al stream;
\item[\lct{print:}] agrega la representaci\'on textual del par\'ametro al stream.
	\cmindex{Stream}{print:}
\end{description}

Tambi\'en hay m\'etodos \'utiles para imprimir diferentes tipos de caracteres en el stream, 
como \mthind{WriteStream}{space}, \mthind{WriteStream}{tab} y \mthind{WriteStream}{cr} (carriage return).
Otro m\'etodo de utilidad es \mthind{WriteStream}{ensureASpace}, que asegura que el \'ultimo caracter
en el stream es un espacio; si el \'ultimo caracter no es un espacio, agrega uno. 

\paragraph{Sobre la Concatenaci\'on.}
Usar \mthind{WriteStream}{nextPut:} y \mthind{WriteStream}{nextPutAll:} en un \ct{WriteStream} es a menudo la mejora forma de concatenar caracteres. Usar el operador de concatenaci\'on por comas (\ct{,}) es mucho menos eficiente. 

\begin{code}{}
[| temp |
  temp := String new.
  (1 to: 100000)
    do: [:i | temp := temp, i asString, ' ']] timeToRun --> 115176 "(milliseconds)"

[| temp |
  temp := WriteStream on: String new.
  (1 to: 100000)
    do: [:i | temp nextPutAll: i asString; space].
  temp contents] timeToRun --> 1262 "(milliseconds)"
\end{code}

to help you do this:
La raz\'on por la que usar un stream puede ser mucho m\'as eficiente es que la coma crea una nuva cadena de caracteres conteniendo la concatenaci\'on del receptor del mensaje y el argumento, por lo que tiene que copiar ambos.
Cuando concatenas repetidamente en el mismo receptor, se hace cada vez m\'as largo, por lo que el n\'umero de caracteres que hay que copiar crece exponencialmente. 
Esto tambi\'en crea mucha basura, que debe ser recolectada. Usar un stream en lugar de concatenaci\'on de cadenas es una optimizaci\'on conocida.
%No traduzco el comentario de lr


\begin{code}{}
String streamContents: [ :tempStream |
  (1 to: 100000)
       do: [:i | tempStream nextPutAll: i asString; space]] 
\end{code}

%-----------------------------------------------------------------
\subsection{Leyendo y escribiendo al mismo tiempo}

Es posible usar un stream para acceder a una colecci\'on para leer y escribir al mismo tiempo. 
Imagina que quieres crear una clase \ct{History} que manejar\'a los botones adelante y atr\'as en un navegador web.
El historial reaccionar\'a como en las figuras \ref{fig:emptyStream} a \ref{fig:page4}.

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{emptyStef}}
\caption{El nuevo historial est\'a vac\'io. No se muestra nada en el navegador web.}
\figlabel{emptyStream}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page1Stef}}
\caption{El usuario abre la p\'agina 1.}
\figlabel{page1}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page2Stef}}
\caption{El usuario hace clic en un enlace a la p\'agina 2.}
\figlabel{page2}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page3Stef}}
\caption{El usuario hace clic en un enlace a la p\'agina 3.}
\figlabel{page3}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page2_Stef}}
\caption{El usuario hace clic en el bot\'on atr\'as. Ahora est\'a viendo la p\'agina 2 nuevamente.}
\figlabel{page2_}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page1_Stef}}
\caption{El usuario hace clic nuevamente en el bot\'on atr\'as. Ahora se muestra la p\'agina 1.}
\figlabel{page1_}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page4Stef}}
\caption{Desde la p\'agina 1, el usuario hace clic en un enlace a la p\'agina 4. El historial olvida las p\'aginas 2 y 3.}
\figlabel{page4}
\vspace{.2in}
\end{figure}

\noindent Este comportamiento puede ser implementado usando un \clsind{ReadWriteStream}.

%%%%%REVISAR%%%%%

\needlines{6}
\begin{code}{}
Object subclass: #History
  instanceVariableNames: 'stream'
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PBE-Streams'

History>>initialize
    super initialize.
    stream := ReadWriteStream on: Array new.
\end{code}

Nada realmente dif\'icil, definimos una nueva clase que contiene un stream. El stream es creado durante el m\'etodo \ct{initialize}.
Necesitamos m\'etodos para ir hacia adelante y hacia atr\'as.

\begin{code}{}
History>>goBackward
  self canGoBackward ifFalse: [self error: 'Already on the first element'].
  stream skip: -2.
  ^ stream next.

History>>goForward
  self canGoForward ifFalse: [self error: 'Already on the last element'].
  ^ stream next
\end{code}

Hasta ac\'a, el c\'odigo era bastante directo. Ahora tenemos que tratar con el m\'etodo \ct{goTo:}, que deber\'ia 
ser activado cuando el usuario hace clic en un enlace. Una posible soluci\'on es:

\begin{code}{}
History>>goTo: aPage
    stream nextPut: aPage.
\end{code}

Sin embargo esta vers\'ion est\'a incompleta. Esto es porque cuando el usuario
hace clic en el enlace, no deber\'ia haber m\'as p\'aginas donde ir hacia adelante, 
\ie el bot\'on adelante debe ser desactivado. Para esto, la soluci\'on m\'as simple
es escribir \ct{nil} justo despu\'es para indicar el final del historial.

\begin{code}{}
History>>goTo: anObject
  stream nextPut: anObject.
  stream nextPut: nil.
  stream back.
\end{code}

Ahora, solo queda implementar los m\'etodos \ct{canGoBackward} y \ct{canGoForward}.

Un stream siempre se posiciona entre dos elementos. Para ir hacia atr\'as,
debe haber dos p\'aginas antes de la posici\'on actual: una es la p\'agina actual,
y la otra es la p\'agina a la que queremos ir. 

\begin{code}{}
History>>canGoBackward
  ^ stream position > 1

History>>canGoForward
  ^ stream atEnd not and: [stream peek notNil]
\end{code}

Agreguemos un m\'etodo para ver los cotenidos del stream:
\begin{code}{}
History>>contents
  ^ stream contents
\end{code}

Y el historial funciona como anunciamos:
\begin{code}{}
History new
	goTo: #page1;
	goTo: #page2;
	goTo: #page3;
	goBackward;
	goBackward;
	goTo: #page4;
	contents --> #(#page1 #page4 nil nil)
\end{code}

%=============================================================
\section{Usando streams para acceso a archivos}

Ya has visto como usar streams con colecciones de elementos. Tambi\'en es posible
usar streams con archivos en tu disco. 
Una vez creado, un stream en un archivo es como un stream en una colecci\'on:
podr\'as usar el mismo protocolo para leer, escribir o posicionar el stream.
La diferencia m\'as importante aparece en la creaci\'on del stream.
Hay muchas formas diferentes de crear streams de archivos, como veremos a continuaci\'on.
 
%-----------------------------------------------------------------
\subsection{Creating file streams}
\subsection{Creando streams de archivos}
\seclabel{creat-file-stre}

Para crear streams de archivos, tendr\'as que usar uno de los 
m\'etodos de instanciaci\'on que ofrece la clase \clsindmain{FileStream}:

\begin{description}

\item[\lct{fileNamed:}] Abre un archivo con el nombre dado para lectura y escritura.
    Si el archivo existe, su contenido previo puede ser modificado o reemplazado, pero 
    el archivo no ser\'a truncado al cerrarse. Si el nombre no contiene la ruta, el archivo 
    se crear\'a en el directorio por defecto.    
    \cmindex{FileStream class}{fileNamed:}

\item[\lct{newFileNamed:}] Crea un nuevo archivo con el nombre dado, y devuelve
    un stream abierto para escritura en ese archivo. Si el archivo ya existe
    pregunta al usuario que hacer.
  \cmindex{FileStream class}{newFileNamed:}

\item[\lct{forceNewFileNamed:}] Crea un nuevo archivo con el nombre dado, y devuelve
    un stream abierto para escritura en ese archivo. Si el archivo ya existe lo borra 
    sin preguntar.
    \cmindex{FileStream class}{forceNewFileNamed:}

\item[\lct{oldFileNamed:}] Abre un archivo existente con el nombre dado para 
    lectura y escritura. Si el archivo existe, su contenido previo puede ser
    modificado o reemplazado, pero el archivo no ser\'a truncado al cerrarse.
    Si el nombre no especifica la ruta, el archivo se crear\'a en el directorio por defecto.
    \cmindex{FileStream class}{oldFileNamed:}

\item[\lct{readOnlyFileNamed:}] Abre un archivo existente con el nombre dado solo para lectura.
    \cmindex{FileStream class}{readOnlyFileNamed:}

\end{description}

Recuerda que cada vez que abres un stream sobre un archivo, tambi\'en debes cerrarlo. 
Esto se hace con el m\'etodo \mthind{FileStream}{close}.

\begin{code}{@TEST |stream|}
stream := FileStream forceNewFileNamed: 'test.txt'.
stream
    nextPutAll: 'This text is written in a file named ';
    print: stream localName.
stream close.

stream := FileStream readOnlyFileNamed: 'test.txt'.
stream contents. --> 'This text is written in a file named ''test.txt'''
stream close.
\end{code}

%[:fileName | (FileDirectory forFileName: fileName)
%	deleteFileNamed: fileName
%	ifAbsent: [ 'Could not delete ', fileName ] ]
%	value: 'test.txt'

El m\'etodo \mthind{FileStream}{localName} devuelve el \'ultimo componente del nombre del archivo. 
Puedes acceder al nombre completo con el m\'etodo \mthind{StandardFileStream}{fullName}.
 
Pronto notar\'as que cerrar los stream de archivos manualmente es doloroso y propenso a errores. 
Es por eso que ct{FileStream} ofrece un mensaje llamado \mthind{FileStream class}{forceNewFileNamed:do:} 
para cerrar autom\'aticamente un nuevo stream luego de evaluar un bloque que modifica sus contenidos. 

 
\begin{code}{@TEST |string|}
FileStream
    forceNewFileNamed: 'test.txt'
    do: [:stream |
        stream
            nextPutAll: 'This text is written in a file named ';
            print: stream localName].
string := FileStream
            readOnlyFileNamed: 'test.txt'
            do: [:stream | stream contents].
string --> 'This text is written in a file named ''test.txt'''
\end{code}

Los m\'etodos de creaci\'on de streams que toman un bloque como argumento
primero crean un stream sobre un archivo, luego ejecutan el bloque con 
el stream como argumento, y finalmente cierran el stream. Estos m\'etodos
devuelven lo que es devuelto por el bloque, o sea, el valor de la \'ultima
expresi\'on del bloque. Esto es usado en el ejemplo anterior para obtener
el contenido del archivo y colocarlo en la variable \ct{string}.

%-----------------------------------------------------------------
\subsection{Binary streams}
\subsection{Streams binarios}
\seclabel{binary-streams}

Por defecto, los streams se crean en texto, lo que significa que leer\'as 
y escribir\'as caracteres. Si tu stream debe ser binario, 
tienes que enviar el mensaje \mthind{FileStream}{binary} a tu stream.

Cuando tu stream est\'a en modo binario, solo puedes escribir n\'umeros
desde 0 a 255 (1 byte). Si quieres usar \ct{nextPutAll:} para escribir
m\'as de un n\'umero al mismo tiempo, tienes que pasar un \clsind{ByteArray} como argumento. 
 
\begin{code}{@TEST}
FileStream
  forceNewFileNamed: 'test.bin'
  do: [:stream |
          stream
            binary;
            nextPutAll: #(145 250 139 98) asByteArray].

FileStream
  readOnlyFileNamed: 'test.bin'
  do: [:stream |
          stream binary.
          stream size.         --> 4
          stream next.         --> 145
          stream upToEnd. --> #[250 139 98]
      ].
\end{code}

Aqu\'i hay otro ejemplo que crea una im\'agen en un archivo llamado
``test.pgm`` (formato de archivo Portable Graymap). Puedes abrir este archivo con tu programa de dibujo favorito.

\begin{code}{@TEST}
FileStream
  forceNewFileNamed: 'test.pgm' 
  do: [:stream |
	stream
		nextPutAll: 'P5'; cr;
		nextPutAll: '4 4'; cr;
		nextPutAll: '255'; cr;
		binary;
		nextPutAll: #(255 0 255 0) asByteArray;
		nextPutAll: #(0 255 0 255) asByteArray;
		nextPutAll: #(255 0 255 0) asByteArray;
		nextPutAll: #(0 255 0 255) asByteArray
	]
\end{code}

Esto crea un checkerboard de 4x4, como se ve en \figref{checkerboard4x4}.

\begin{figure}[!ht]
\centerline{\includegraphics[width=0.25\textwidth]{checkerboard4x4}}
\caption{Un checkerboard de 4x4 que puedes dibujar usando streams binarios.}
\figlabel{checkerboard4x4}
\vspace{.2in}
\end{figure}

%=============================================================
\section{Res\'umen del cap\'itulo}

Los streams ofrecen una mejor manera que las colecciones para leer y escribir incrementalmente una secuencia de elementos.
Hay maneras sencillas de convertir streams en colecciones y viceversa.

\begin{itemize}
  \item Los streams pueden ser de escritura, de lectura o ambos al mismo tiempo. 
  \item Para convertir una colecci\'on en un stream, define un stream ``en'' una colecci\'on, \eg \ct{ReadStream on: (1 to: 1000)}, o env\'ia los mensajes \ct{readStream}, \etc a la colecci\'on.
  \item Para convertir un stream en una colecci\'on, env\'ia el mensaje \ct{contents}.
  \item para concatenar colecciones largas, en lugar de usar el operador coma, es m\'as eficiente crear un stream, anexar las colecciones al stream con \ct{nextPutAll:}, y extraer el resultado enviando \ct{contents}.
  \item Los streams de archivos son por defecto basados en texto. Env\'ia \ct{binary} para hacerlos binarios expl\'icitamente.
\end{itemize}

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables: 
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End:
